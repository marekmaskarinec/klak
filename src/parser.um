
import (
	"std.um"
	"gen.um"	
	"lexer.um"
	"common.um"
	"../lib/libs/list.um"
)

type ErrArgs = []interface{}

type Parser* = struct {
	l: lexer.Lexer
	g: gen.Gen

	err_fn: fn(msg: str, values: []interface{}, lineno, charno: int)
	lno: int
	cno: int
	had_error: bool

	words: []str
	builtins: []str

	if_nest_size: int
	loop_nest_size: int

	// case
	case_nest_size: int
	on_nest_size: int
	default_nest_size: int
	in_case: bool
	on_count: []int

	variables: list.List
}

fn (p: ^Parser) parse_next(stop: int, stop_str: str): bool

fn (p: ^Parser) err(msg: str, args: ErrArgs) {
	p.had_error = true

	p.err_fn(msg, args, p.lno, p.cno)
}

fn (p: ^Parser) is_builtin(word: str): bool {
	for w in p.builtins {
		if w == word {
			return true
		}
	}

	return false
}

fn (p: ^Parser) is_user_word(word: str): bool {
	for w in p.words {
		if w == word {
			return true
		}
	}

	return false
}

fn (p: ^Parser) is_variable(word: str): bool {
	for node:=p.variables.front;
	    node != null && ^str(node.value)^ != "ENTER_FUNCTION";
	    node=node.next {
		
		if word == ^str(node.value)^ {
			return true
		}
	}

	return false
}

// TODO: do this in more elegant way
fn (p: ^Parser) decode_char_word(word: str): char {
	if word == "space" {
		return ' '
	}

	if word == "newline" {
		return '\n'
	}

	p.err("Incorrect char word ~a.", ErrArgs{word})
	return '\0'
}

fn (p: ^Parser) hex_to_int(hex: str): int {
	sum := 0

	for i:=len(hex)-1; i >= 0; i-- {
		char_val := 0

		if hex[i] >= '0' && hex[i] <= '9' {
			char_val = int(hex[i]) - int('0')
		} else if hex[i] >= 'a' && hex[i] <= 'f' {
			char_val = int(hex[i]) - int('a') + 10
		} else {
			p.err("Incorrect hex number ~a.", ErrArgs{hex})
		}

		sum += char_val * common.pow(16, (len(hex) - i - 1))
	}

	return sum
}

fn is_float_valid(num: str): bool {
	dotc := 0

	for i, c in num {
		if c == '.' {
			dotc++
			if i == 0 {
				return false
			}
		}
	}

	return dotc <= 1
}

fn (p: ^Parser) parse_mkw() {
	t := p.l.next()

	if t.t != lexer.tok_word {
		p.err("Expected word.", ErrArgs{})
		return
	}

	exists := false
	for w in p.words {
		if w == t.v {
			exists = true
			break
		}
	}

	if !exists {
		p.words = append(p.words, t.v)
	}

	p.g.word_decl(t.v)

	t = p.l.peek()
	if t.t == lexer.tok_lambda_close {
		p.g.buf.write_str(";\n")
	} else {
		p.g.open()
		p.variables.push_front("ENTER_FUNCTION")
		p.variables.push_front("ENTER_SCOPE")
	}
}

fn (p: ^Parser) parse_if() {
	p.variables.push_front("ENTER_SCOPE")
	for p.parse_next(lexer.tok_keyword, "then") && !p.had_error { }
	p.variables = p.g.gc(p.variables)

	p.variables.push_front("ENTER_SCOPE")
	p.g.if_cond()
	p.if_nest_size++
}

fn (p: ^Parser) parse_else() {
	if p.in_case {
		p.variables.push_front("ENTER_SCOPE")
		p.default_nest_size++

		return
	}

	if p.if_nest_size == 0 {
		p.err("Unexpected else. Not in a statement.", ErrArgs{})
	}

	p.g.else_cond()
}

fn (p: ^Parser) parse_fi() {
	if p.if_nest_size == 0 {
		p.err("Unexpected fi. Not in a statement.", ErrArgs{})
	}

	p.if_nest_size--
	p.variables = p.g.gc(p.variables)
	p.g.fi_cond()
}

fn (p: ^Parser) parse_loop() {
	p.g.loop_head()

	p.variables.push_front("ENTER_SCOPE")
	for p.parse_next(lexer.tok_keyword, "then") && !p.had_error { }
	p.variables = p.g.gc(p.variables)

	p.g.loop_cond()
	p.g.lower_indent()
	p.g.write(p.g.indent + "}\n")

	p.loop_nest_size++
}

fn (p: ^Parser) parse_pool() {
	p.loop_nest_size--
	p.variables = p.g.gc(p.variables)
	p.g.pool()
}

fn (p: ^Parser) parse_case() {
	p.g.case_header()
	
	p.variables.push_front("ENTER_SCOPE")
	for p.parse_next(lexer.tok_keyword, "then") && !p.had_error { }
	p.variables = p.g.gc(p.variables)

	p.g.case_footer(p.case_nest_size)

	p.case_nest_size++
	p.in_case = true

	p.on_count = append([]int{0}, p.on_count)
}

fn (p: ^Parser) parse_on() {
	if !p.in_case {
		p.err("On not used in a case statement.", ErrArgs{})
	}

	p.g.write("\n" + p.g.indent + "{\n")
	p.g.indent += "\t"

	p.variables.push_front("ENTER_SCOPE")
	for p.parse_next(lexer.tok_keyword, "then") && !p.had_error { }
	p.variables = p.g.gc(p.variables)

	p.g.on(p.case_nest_size)

	p.variables.push_front("ENTER_SCOPE")

	p.in_case = false
	p.on_nest_size++
	p.on_count[0]++
}

fn (p: ^Parser) parse_no() {
	if p.on_nest_size == 0 {
		p.err("Unexpected no. Not in an on statement.", ErrArgs{})
	}

	p.variables = p.g.gc(p.variables)
	p.on_nest_size--
	p.in_case = true

	p.g.no()
}

fn (p: ^Parser) parse_esac() {
	if p.case_nest_size == 0 {
		p.err("Not in a case statement.", ErrArgs{})
		return
	}

	if p.default_nest_size == 0 {
		p.err("Missing default case.", ErrArgs{})
		return
	}

	for i:=0; i < p.on_count[0]; i++ {
		p.g.lower_indent()
		p.g.write(p.g.indent + "}\n")
	}

	if len(p.on_count) == 1 {
		p.on_count = []int{}
	} else {
		p.on_count = slice(p.on_count, 1, len(p.on_count))
	}

	// exit case scope
	p.variables = p.g.gc(p.variables)
	p.g.lower_indent()
	p.g.write(p.g.indent + "}\n")

	p.case_nest_size--
	p.in_case = false
}

fn (p: ^Parser) parse_ret() {
	if !p.g.in_block {
		p.err("ret used outsize of word definition.", ErrArgs{})
	}

	p.g.gc(p.variables)
	p.g.ret()
}

fn (p: ^Parser) parse_break() {
	if p.loop_nest_size == 0 {
		p.err("No loop to break.", ErrArgs{})
	}

	p.g.gc(p.variables)
	p.g.break_kw()
}

fn (p: ^Parser) parse_skip() {
	if p.loop_nest_size == 0 {
		p.err("No loop to skip.", ErrArgs{})
	}

	p.g.gc(p.variables)
	p.g.skip()
}

fn (p: ^Parser) parse_keyword(kw: str) {
	if kw == "if" {
		p.parse_if()
	} else if kw == "else" {
		p.parse_else()
	} else if kw == "fi" {
		p.parse_fi()
	} else if kw == "loop" {
		p.parse_loop()
	} else if kw == "pool" {
		p.parse_pool()
	} else if kw == "case" {
		p.parse_case()
	} else if kw == "on" {
		p.parse_on()
	} else if kw == "no" {
		p.parse_no()
	} else if kw == "esac" {
		p.parse_esac()
	} else if kw == "ret" {
		p.parse_ret()
	} else if kw == "break" {
		p.parse_break()
	} else if kw == "skip" {
		p.parse_skip()
	}
}

fn (p: ^Parser) parse_word(word: str) {
	if p.is_builtin(word) {
		p.g.call_builtin(word)
	} else if p.is_user_word(word) {
		p.g.call_user_word(word)
	} else if p.is_variable(word) {
		p.g.push_variable(word)
	} else {
		p.err("Unknown identifier ~a.", ErrArgs{word})
	}
}

fn (p: ^Parser) parse_next(stop: int, stop_str: str): bool {
	tok := p.l.next()	
	//tok.print()

	p.g.lno = p.lno

	if tok.t == stop && stop_str == tok.v {
		return false
	}

	p.lno = tok.lineno
	p.cno = tok.charno

	switch tok.t {
	case lexer.tok_literal_char:
		p.g.push_simple(tok.v, "kk_type_char", "front", 0)

	case lexer.tok_literal_char_word:
		val := p.decode_char_word(tok.v)
		p.g.push_simple(val, "kk_type_char", "front", 0)
	
	case lexer.tok_literal_char_number:
		p.g.push_simple(char(p.hex_to_int(tok.v)), "kk_type_char", "front", 0)
	
	case lexer.tok_int:
		p.g.push_simple(std.atoi(tok.v), "kk_type_float", "front", 0)

	case lexer.tok_int_hex:
		p.g.push_simple(
			p.hex_to_int(slice(tok.v, 2, len(tok.v))),
			"kk_type_float", "front", 0)

	case lexer.tok_float:
		if !is_float_valid(tok.v) {
			p.err("Invalid float number.", ErrArgs{})
		}

		float_val := std.atof(tok.v)
		p.g.push_simple(float_val, "kk_type_float", "front", 0)

	case lexer.tok_pop:
		p.g.pop(tok.num_mod)

	case lexer.tok_var_assign:
		if !p.is_variable(tok.v) {
			p.err("Unknown identifier ~a.", ErrArgs{tok.v})
		}
		p.g.assign(tok.v)

	case lexer.tok_var_decl:
		if p.is_variable(tok.v) {
			p.err("Variable ~a already exists.", ErrArgs{tok.v})
		}
		p.g.decl(tok.v)	
		p.variables.push_front(tok.v)

	case lexer.tok_literal_string:
		p.g.push_string(tok.v, "front", 0)

	case lexer.tok_keyword:
		p.parse_keyword(tok.v)

	case lexer.tok_lambda_close:
		p.g.close()
		p.variables = p.g.gc(p.variables)
		p.variables.remove(p.variables.front)

	case lexer.tok_constant:
		p.g.constant(tok.v)

	case lexer.tok_word:
		p.parse_word(tok.v)

	case lexer.tok_file:
		p.g.file_mark(tok.v)

	case lexer.tok_mkw:
		p.parse_mkw()

	default:
		p.err("Unimplemented feature ~a.", ErrArgs{tok.t})
		tok.print()
	}

	return true
}
