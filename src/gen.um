
import (
	"../lib/libs/list.um"
	"../lib/libs/strings.um"
)

type Gen* = struct {
	prototypes: []str
	buf: strings.builder
	main_fn: strings.builder
	indent: str
	in_block: bool
}

const (
	var_prefix* = "USER_VAR_"
	word_prefix* = "USER_WORD_"
	builtin_prefix* = "kk_BUILTIN_"
)

fn (g: ^Gen) write(value: str) {
	if g.in_block {
		g.buf.write_str(value)
	} else {
		g.main_fn.write_str(value)
	}
}

fn (g: ^Gen) lno(lno: int) {
	g.write(g.indent + "kk_line = " + repr(lno) + ";\n")
}

fn (g: ^Gen) lower_indent() {
	if len(g.indent) > 1 {
		g.indent = slice(g.indent, 1, len(g.indent))
	} else {
		g.indent = ""
	}
}

fn (g: ^Gen) push_simple(value: interface{}, type_str: str, side: str, offset: int) {
	field := strings.trimprefix(type_str, "kk_type_") + "_val"
	g.write(g.indent + "kk_list_push_" + side +
		"(&the_stack, (kk_cell){ .type = " + type_str + ", ." +
		field + " = " + repr(value) + "}, " + repr(offset) + ");\n\n")
}

fn (g: ^Gen) push_string(value, side: str, offset: int) {
	g.write(g.indent + "{\n"+
		g.indent + "\t" + "kk_gcobj *tmp = malloc(sizeof(kk_gcobj));\n" +
		g.indent + "\t" + "if (!tmp) kk_runtime_error(\"Could not allocate a gc object.\");\n" +
		g.indent + "\t" + "tmp->type = kk_type_string; tmp->refs = 0; \n" +
		g.indent + "\t" + "tmp->data = malloc(" + repr(len(value) + 1) + ");\n" +
		g.indent + "\t" + "if (!tmp->data) kk_runtime_error(\"Could not allocate a string.\");\n" +
		g.indent + "\t" + "((char *)tmp->data)[" + repr(len(value)) + "] = 0;\n" +
		g.indent + "\t" + "strcpy(tmp->data, \"" + value + "\");\n" +
		g.indent + "\t" + "kk_list_push_" + side + "(&the_stack, " +
		"(kk_cell){ .type = kk_type_gcobj, .ptr_val = tmp }, 0);\n" +
		g.indent + "}\n\n")
}

fn (g: ^Gen) pop(n: int) {
	g.write(
		g.indent + "kk_gcobj_dec(&the_stack->cell);\n" +
		g.indent + "kk_list_popn(&the_stack, " + repr(n) + ");\n\n")
}

fn (g: ^Gen) assign(name: str) {
	g.write(g.indent + "kk_cell_copy(&" + var_prefix + name + ", &the_stack->cell);\n")
	g.pop(1)
}

fn (g: ^Gen) decl(name: str) {
	g.write(g.indent + "kk_cell " + var_prefix + name + " = {0};\n\n")
}

fn (g: ^Gen) word_decl(name: str) {
	g.buf.write_str("void " + word_prefix + name + "()")
}

fn (g: ^Gen) constant(name: str) {
	if name == "null" {
		g.write(g.indent + "kk_list_push_front(&the_stack, (kk_cell){}, 0);\n")
	}
}

fn (g: ^Gen) call_builtin(name: str) {
	g.write(g.indent + builtin_prefix + name + "();\n\n")
}

fn (g: ^Gen) call_user_word(name: str) {
	g.write(g.indent + word_prefix + name + "();\n\n")
}

fn (g: ^Gen) push_variable(name: str) {
	g.write(g.indent + "kk_list_push_front(&the_stack, " + var_prefix + name + ", 0);\n\n")
}

fn (g: ^Gen) open() {
	g.in_block = true
	g.buf.write_str(" {\n")
}

fn (g: ^Gen) close() {
	g.buf.write_str("}\n\n")
	g.in_block = false
}

fn (g: ^Gen) if_cond() {
	g.write(
		g.indent + "tmp_cell = kk_list_popget(&the_stack);\n" +
		g.indent + "tmp_res = kk_is_true(tmp_cell);\n" +
		g.indent + "kk_gcobj_dec(&tmp_cell);\n" +
		g.indent + "if (tmp_res) {\n")

	g.indent += "\t"
}

fn (g: ^Gen) else_cond() {
	g.lower_indent()
	g.write(g.indent + "} else {\n")
	g.indent += "\t"
}

fn (g: ^Gen) fi_cond() {
	g.lower_indent()
	g.write(g.indent + "}\n\n")
}

fn (g: ^Gen) gc_var(name: str) {
	name = var_prefix + name
	g.write(
		g.indent + "if (" + name + ".type == kk_type_gcobj)\n" +
		g.indent + "\t" + "kk_gcobj_dec(name);\n")
}

fn (g: ^Gen) gc(vars: list.List): list.List {
	for node:=vars.front;
		node != null && ^str(node.value)^ != "ENTER_SCOPE";
		node=vars.front {

		g.gc_var(^str(node.value)^)
		vars.remove(vars.front)
	}

	return vars
}

fn (g: ^Gen) loop_head() {
	g.write(g.indent + "for (;;) {\n" +
		g.indent + "\t" + "{\n")

	g.indent += "\t\t"
}

fn (g: ^Gen) loop_cond() {
	g.write(
		g.indent + "tmp_cell = kk_list_popget(&the_stack);\n" +
		g.indent + "tmp_res = kk_is_true(tmp_cell);\n" +
		g.indent + "kk_gcobj_dec(&tmp_cell);\n" +
		g.indent + "if (!tmp_res) break;\n")
}

fn (g: ^Gen) pool() {
	g.lower_indent()
	g.write(g.indent + "}\n")
}

fn (g: ^Gen) case_header() {
	g.write(
		g.indent + "{\n" +
		g.indent + "\t{\n")
	g.indent += "\t\t"
}

fn (g: ^Gen) case_footer(nest: int) {
	g.lower_indent()
	g.write(
		g.indent + "}\n\n" +
		g.indent + "kk_cell case_tmp_" + repr(nest + 1) + " = kk_list_popget(&the_stack);\n" +
		g.indent)
}

fn (g: ^Gen) on(nest: int) {
	g.lower_indent()
	g.write(
		g.indent + "}\n" +
		g.indent + "kk_list_push_front(&the_stack, case_tmp_" + repr(nest) + ", 0);\n" +
		g.indent + "kk_BUILTIN___EQUAL__();\n" +
		g.indent + "tmp_cell = kk_list_popget(&the_stack);\n" +
		g.indent + "tmp_res = kk_is_true(tmp_cell);\n" +
		g.indent + "kk_gcobj_dec(&tmp_cell);\n" +
		g.indent + "if (!tmp_res) {\n")
	g.indent += "\t"
}

fn (g: ^Gen) no() {
	g.lower_indent()
	g.write(g.indent + "} else {\n")
	g.indent += "\t"
}

fn (g: ^Gen) ret() {
	g.write(g.indent + "return;\n")
}

fn (g: ^Gen) break_kw() {
	g.write(g.indent + "break;\n")
}

fn (g: ^Gen) skip() {
	g.write(g.indent + "continue;\n")
}

fn (g: ^Gen) file_mark(filename: str) {
	g.write(g.indent + "strcpy(kk_file, \"" + filename  + "\");\n")
}

fn (g: ^Gen) print() {
	printf("%s\n", g.buf.to_str())
	printf("int main() {\n%s\n}\n", g.main_fn.to_str())
}
